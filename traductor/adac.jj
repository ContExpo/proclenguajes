//*****************************************************************
// File:   adac.jj
// Author: ...........
// Date:   ...........
// Coms:   Un tutorial sobre el análisis léxico con javacc
//         https://javacc.github.io/javacc/tutorials/token-manager.html
//		   Para ejecutar el jar: java -jar ruta_jar < ruta_prog_prueba
//*****************************************************************

options {
  // IGNORE_CASE = false;
  // COMMON_TOKEN_ACTION = false;
  DEBUG_PARSER = true;
}

PARSER_BEGIN(adac)
package traductor;
import java.util.Set;
import java.util.HashSet;
import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;

public class adac {

	static SymbolTable st;

	private static void initSymbolTable() {
		boolean b;
		String[] palsRes = {
	    	"var","const","escribir"
		};

		st.insertReservedWords(palsRes);
	}

    public static void main(String[] args) {
    	adac parser;

    	try {
	    	if(args.length == 0) { //entrada desde stdin
				parser = new adac(System.in);
			}
			else { //entrada desde fichero en args[0]
	            parser = new adac(new java.io.FileInputStream(args[0])); 
			}
			parser.S(); //invoca símbolo inicial de la gramática
		}
		catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + args[0] + " no encontrado.");
		}
		catch (TokenMgrError e) {
        	System.err.println("LEX_ERROR: " + e.getMessage());
        }
        catch (ParseException e) {
        	System.err.println("SINT_ERROR: " + e.getMessage());
        } 
    }
}
PARSER_END(adac)
//------------ descripción del AL

TOKEN_MGR_DECLS : 
{
	static void printToken (Token t, String s) {
		System.out.printf("(%d, %d): \"%s\" %s\n", t.beginLine,
		t.beginColumn, t.image, s );
	}
}

TOKEN :
{
 	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
|	< #NUM : (<DIGIT>)+ >
|	< #WORD: ["a"-"z","A"-"Z"](~[" ","\t","\n","\r","0"-"9"])*>
}

SKIP :
{
	
	< " " | "\t" | "\r" | "\n" >	// Espacios en blanco
|	< "--"(~["\n", "\r"])* >				// Comentarios
}

TOKEN :
{
	< tCHAR: "'" (~["\n","\r","'"]) "'" >
|	< tSTRING: "\"" (~["\n","\r", "\""] | "\"\"")* "\"" >
|	< tNUM: <NUM> >
|	< tPROC: "procedure" >
|	< tIS: "is" >
| 	< tBEGIN: "begin" >
|	< tEND: "end" >
|	< tRET: "return" >
|	< tBOOL: "boolean" >
|	< tCHDECL: "character" >
|   < tINT:  "integer" >
|   < tNOT:  "not" >
|   < tOR:  "or" >
|   < tAND:  "and" >
|   < tIF:  "if" >
|   < tTHEN:  "then" >
|   < tELSE:  "else" >
|   < tWHIL:  "while" >
|   < tDO:  "do" >
|	< tCOMMA:  "," >
|	< tSCOL:  ";" >
|	< tPTOP:  "(" >
|	< tPTCL:  ")" >
|	< tSQOP:  "[" >
|	< tSQCL:  "]" >
|	< tSQUOT:  "'" >
|	< tDQUOT:  "\"" >
|   < tASIG:  ":=" >
|	< tADD:  "+" >
|	< tSUB:  "-" >
|	< tMUL:  "*" >
|	< tDIV:  "div" >
|	< tMOD:  "mod" >
|	< tGT:  ">" >
|	< tLT:  "<" >
|	< tEQ:  "=" >
|	< tGEQ:  ">=" >
|	< tLEQ:  "<=" >
|	< tDIFF:  "<>" >
|	< tTRUE:  "true" >
|	< tFALSE:  "false" >
|	< tATOI:  "char2int" >
|	< tITOA:  "int2char" >
|	< tGET:  "get" >
|	< tSKIPL:  "skip_line" >
|	< tPUT:  "put" >
|	< tPUTL:  "put_line" >
|	< tFUNC: "function" >
|	< tVAL: "val" >
|	< tREF: "ref" >
|	< tID: <LETTER> ( ("_")? (<DIGIT> | <LETTER>) )* >
}
SPECIAL_TOKEN:
{
	< ~[] >
		{
			System.out.print("LEXICAL ERROR: ");
			System.out.printf("(%d, %d): unrecognized symbol: %s", matchedToken.beginLine,
			matchedToken.beginColumn, matchedToken.image );
		}
}


//------------ Símbolo inicial de la gramática. Para análisis léxico no hace falta más
void S () : 
{

}
{
	try {
		<tPROC> <tID> <tIS>
		declaracion_variables() declaracion_procs_funcs() bloque_sentencias() <EOF>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		//conjSinc.add(token);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_variables : ( declaracion <tSCOL> )*
void declaracion_variables() :
{
	
}
{
	try {
		(declaracion() <tSCOL>)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion : tipo_variable lista_vars
void declaracion() :
{
	
}
{
	try {
		tipo_variable() lista_vars()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

// tipo_variable : <tINT> | <tCHDECL> | <tBOOL>
void tipo_variable() :
{
	
}
{
	try {
		<tINT> | <tCHDECL> | <tBOOL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//lista_vars : variable ( <tCOMMA> variable )*
void lista_vars() :
{
	
}
{
	try {
		variable() ( <tCOMMA> variable() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//variable : LOOKAHEAD(2)
//				<tID> <tACOR> <tCONST_INT> <tCCOR>
//			|	<tID>
void variable() :
{
	
}
{
	try {
		LOOKAHEAD(2)
		<tID> <tSQOP> <tNUM> <tSQCL>
	|	<tID>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_procs_funcs : ( declaracion_proc_func )*
void declaracion_procs_funcs() :
{
	
}
{
	try {
		( declaracion_proc_func() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_proc_func :	declaracion_proceso
//						|	declaracion_funcion
void declaracion_proc_func() :
{
	
}
{
	try {
		declaracion_proceso()
	|	declaracion_funcion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_proceso : cabecera_proceso declaracion_variables declaracion_procs_funcs bloque_sentencias
void declaracion_proceso() :
{
	
}
{
	try {
		cabecera_proceso() declaracion_variables() declaracion_procs_funcs() bloque_sentencias()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//cabecera_proceso : <tPROC> <tID> ( decs_parametros )? <tIS>
void cabecera_proceso() :
{
	
}
{
	try {
		<tPROC> <tID> ( decs_parametros() )? <tIS>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}



//declaracion_funcion : cabecera_funcion declaracion_variables declaracion_procs_funcs bloque_sentencias
void declaracion_funcion() :
{
	
}
{
	try {
		cabecera_funcion() declaracion_variables() declaracion_procs_funcs() bloque_sentencias()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//cabecera_funcion : <tFUNC> tipo_variable <tID> ( decs_parametros )? <tIS>
void cabecera_funcion() :
{
	
}
{
	try {
		<tFUNC> tipo_variable() <tID> ( decs_parametros() )? <tIS>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//bloque_sentencias : <tBEGIN> ( instruccion )* <tEND>
void bloque_sentencias() :
{
	
}
{
	try {
		<tBEGIN> ( instruccion() )* <tEND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//decs_parametros : <tPTOP> ( dec_parametros ( <tSCOL> dec_parametros )* )? <tPTCL>
void decs_parametros() :
{
	
}
{
	try {
		<tPTOP> ( dec_parametros() ( <tSCOL> dec_parametros() )* )? <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//dec_parametros : clase_parametros tipo_variable id_or_array ( <tCOMMA> id_or_array )*
void dec_parametros() :
{
	
}
{
	try {
		clase_parametros() tipo_variable() id_or_array() ( <tCOMMA> id_or_array() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//id_or_array : LOOKAHEAD(2)
//					<tID> <tSQOP> <tNUM> <tSQCL>
//				|	<tID>
void id_or_array() :
{
	
}
{
	try {
		LOOKAHEAD(2)
		<tID> <tSQOP> <tNUM> <tSQCL>
	|	<tID>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//clase_parametros:	<tREF>
//				|	<tVAL>
void clase_parametros() :
{
	
}
{
	try {
		<tREF>
	|	<tVAL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//instruccion : 	inst_leer <tSCOL>
//				|	inst_saltar_linea <tSCOL> 
//				|	inst_escribir <tSCOL> 
//				|	inst_escribir_linea <tSCOL> 
//				|LOOKAHEAD(2)
//					inst_invoc_proc <tSCOL> 
//				|	inst_asignacion <tSCOL> 
//				|	inst_seleccion
//				|	inst_iteracion
//				|	inst_return <tSCOL>
void instruccion() :
{
	
}
{
	try {
		inst_leer() <tSCOL>
	|	inst_saltar_linea() <tSCOL> 
	|	inst_escribir() <tSCOL> 
	|	inst_escribir_linea() <tSCOL> 
	|LOOKAHEAD(2)
		inst_invoc_proc() <tSCOL> 
	|	inst_asignacion() <tSCOL> 
	|	inst_seleccion()
	|	inst_iteracion()
	|	inst_return() <tSCOL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_leer : <tGET> <tPTOP> asignable ( <tCOMMA> asignable )* <tPTCL>
void inst_leer() :
{
	
}
{
	try {
		<tGET> <tPTOP> asignable() ( <tCOMMA> asignable() )* <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_saltar_linea : <tSKIPL> <tPTOP> <tPTCL>
void inst_saltar_linea() :
{
	
}
{
	try {
		<tSKIPL> <tPTOP> <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_escribir : <tPUT> <tPTOP> lista_exps <tPTCL>
void inst_escribir() :
{
	
}
{
	try {
		<tPUT> <tPTOP> lista_exps() <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_escribir_linea : <tPUTL> <tPTOP> lista_exps <tPTCL>
void inst_escribir_linea() :
{
	
}
{
	try {
		<tPUTL> <tPTOP> lista_exps() <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_invoc_proc : <tID> <tPTOP> lista_exps <tPTCL>
void inst_invoc_proc() :
{
	
}
{
	try {
		<tID> <tPTOP> lista_exps() <tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		// añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_asignacion : asignable <tASIG> expresion
void inst_asignacion() :
{
	
}
{
	try {
		asignable() <tASIG> expresion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_seleccion :	<tIF> expresion <tTHEN> ( instruccion )* resto_if <tEND>
void inst_seleccion() :
{
	
}
{
	try {
		<tIF> expresion() <tTHEN> ( instruccion() )* ( resto_if() )? <tEND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//resto_if : <tELSE> ( instruccion )*
void resto_if() :
{
	
}
{
	try {
		<tELSE> ( instruccion() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//asignable : LOOKAHEAD(2)
//				<tID> <tSQOP> expresion <tSQCL>
//			|	<tID>
void asignable() :
{
	
}
{
	try {
	LOOKAHEAD(3)
		<tID> <tSQOP> expresion() <tSQCL>
	|	<tID>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_iteracion : <tWHIL> expresion <tDO> ( instruccion )* <tEND>
void inst_iteracion() :
{
	
}
{
	try {
		<tWHIL> expresion() <tDO> ( instruccion() )* <tEND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_return : <tRET> expresion
void inst_return() :
{
	
}
{
	try {
		<tRET> expresion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//lista_exps : ( expresion ( <tCOMMA> expresion )* )?
void lista_exps() :
{
	
}
{
	try {
		( expresion() ( <tCOMMA> expresion() )* )?
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//expresion : expresion_simple ( operador_relacional expresion_simple )?
void expresion() :
{
	
}
{
	try {
		expresion_simple() ( operador_relacional() expresion_simple() )?
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//operador_relacional :	<tEQ>
//					|	<tLT>
//					|	<tGT>
//					|	<tLEQ>
//					|	<tGEQ>
//					|	<tDIFF>
void operador_relacional() :
{
	
}
{
	try {
		<tEQ>
	|	<tLT>
	|	<tGT>
	|	<tLEQ>
	|	<tGEQ>
	|	<tDIFF>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//expresion_simple : ( <tADD> | <tSUB> )? termino ( operador_aditivo termino )*
void expresion_simple() :
{
	
}
{
	try {
		( <tADD> | <tSUB> )? termino() ( operador_aditivo() termino() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//operador_aditivo :	<tADD>
//					|	<tSUB>
//					|	<tOR>
void operador_aditivo() :
{
	
}
{
	try {
		<tADD>
	|	<tSUB>
	|	<tOR>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//termino : factor ( operador_multiplicativo factor )*
void termino() :
{
	
}
{
	try {
		factor() ( operador_multiplicativo() factor() )*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//operador_multiplicativo :	<tMUL>
//						|	<tMOD>
//						|	<tDIV>
//						|	<tAND>
void operador_multiplicativo() :
{
	
}
{
	try {
		<tMUL>
	|	<tMOD>
	|	<tDIV>
	|	<tAND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//factor :	<tNOT> factor
//		|	<tPTOP> expresion <tPTCL>
//		|	<tINT2CHAR> <tPTOP> expresion <tPTCL>
//		|	<tCHAR2INT> <tPTOP> expresion <tPTCL>
//		|	LOOKAHEAD(2)  <tID> <tPTOP> lista_exps <tPTCL>
//		|	LOOKAHEAD(2)  <tID> <tSQOP> expresion <tSQCL>
//		|	<tID>
//		|	<tNUM>
//		|	<tCHAR>
//		|	<tSTRING>
//		|	<tTRUE>
//		|	<tFALSE>
void factor() :
{
	
}
{
	try {
		<tNOT> factor()
	|	<tPTOP> expresion() <tPTCL>
	|	<tITOA> <tPTOP> expresion() <tPTCL>
	|	<tATOI> <tPTOP> expresion() <tPTCL>
	|	LOOKAHEAD(2)  <tID> <tPTOP> lista_exps() <tPTCL>
	|	LOOKAHEAD(2)  <tID> <tSQOP> expresion() <tSQCL>
	|	<tID>
	|	<tNUM>
	|	<tCHAR>
	|	<tSTRING>
	|	<tTRUE>
	|	<tFALSE>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		conjSinc.add(tSQCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

JAVACODE //------------------------------------ 
//Informa sobre la excepción, y calcula el conjunto de tokens esperados Set infoParseException(ParseException e) {
Set<Integer> infoParseException(ParseException e) {
    Set<Integer> esperados = new HashSet<Integer>(); 
    System.err.println("ERROR_SINT: ('" + e.currentToken.next.image + ", " + e.currentToken.next.beginLine
        + "," + e.currentToken.next.beginColumn + ")");
    System.err.println("Se esperaba uno de los siguientes tokens:");
    for(int i = 0; i<e.expectedTokenSequences.length; i++) {
        esperados.add(e.expectedTokenSequences[i][0]);
        System.err.println("\t" + adacConstants.tokenImage[e.expectedTokenSequences[i][0]]);
    }
    return esperados;
}

JAVACODE //--------- 
void recuperacionPanico(Token tInesperado, Set <Integer> conjSinc) {
    // System.err.println(sep); 
    System.err.println("----> Recupero en modo pánico: "
        + "\n----> saltando todo hasta token de conjunto de sincronización"); 
    System.err.println("Lexema inesperado: " + tInesperado.image);
    Token t = getNextToken();
    while (!conjSinc.contains(t. kind) && t.kind != EOF){ 
        System.err.println("Descartando token ("
        + adacConstants.tokenImage[t.kind]
        + "," + t.image + ")"); t = getNextToken();
    }
}