//*****************************************************************
// File:   adac.jj
// Author: ...........
// Date:   ...........
// Coms:   Un tutorial sobre el análisis léxico con javacc
//         https://javacc.github.io/javacc/tutorials/token-manager.html
//		   Para ejecutar el jar: java -jar ruta_jar < ruta_prog_prueba
//*****************************************************************

options {
  // IGNORE_CASE = false;
  // COMMON_TOKEN_ACTION = false;
  DEBUG_PARSER = true;
}

PARSER_BEGIN(adac)
package traductor;
import java.util.Set;
import java.util.HashSet;
import lib.symbolTable.*;
import lib.errores.*;
import lib.attributes.Attributes;
import lib.symbolTable.exceptions.*;
import java.util.ArrayList;

public class adac {

	static SymbolTable st = new SymbolTable();
	static ErrorSemantico es;
	private static void initSymbolTable() {
		boolean b;
		String[] palsRes = {
			"procedure", "function", "val", "ref", "integer", "character", "boolean", "is", "begin", "end",
			"if", "else","while", "then", "return", "mod", "div", "true", "false", "not", "and", "function",
			"get", "char2int", "int2char", "skip_line", "put", "put_line"
		};

		st.insertReservedWords(palsRes);
	}

    public static void main(String[] args) {
    	adac parser;

    	try {
	    	if(args.length == 0) { //entrada desde stdin
				parser = new adac(System.in);
			}
			else { //entrada desde fichero en args[0]
	            parser = new adac(new java.io.FileInputStream(args[0])); 
			}
			parser.S(); //invoca símbolo inicial de la gramática
		}
		catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + args[0] + " no encontrado.");
		}
		catch (TokenMgrError e) {
        	System.err.println("LEX_ERROR: " + e.getMessage());
        }
        catch (ParseException e) {
        	System.err.println("SINT_ERROR: " + e.getMessage());
        } 
    }
}
PARSER_END(adac)
//------------ descripción del AL

TOKEN_MGR_DECLS : 
{
	static void printToken (Token t, String s) {
		System.out.printf("(%d, %d): \"%s\" %s\n", t.beginLine,
		t.beginColumn, t.image, s );
	}
}

TOKEN :
{
 	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
|	< #NUM : (<DIGIT>)+ >
|	< #WORD: ["a"-"z","A"-"Z"](~[" ","\t","\n","\r","0"-"9"])*>
}

SKIP :
{
	
	< " " | "\t" | "\r" | "\n" >	// Espacios en blanco
|	< "--"(~["\n", "\r"])* >				// Comentarios
}

TOKEN :
{
	< tCHAR: "'" (~["\n","\r","'"]) "'" >
|	< tSTRING: "\"" (~["\n","\r", "\""] | "\"\"")* "\"" >
|	< tNUM: <NUM> >
|	< tPROC: "procedure" >
|	< tIS: "is" >
| 	< tBEGIN: "begin" >
|	< tEND: "end" >
|	< tRET: "return" >
|	< tBOOL: "boolean" >
|	< tCHDECL: "character" >
|   < tINT:  "integer" >
|   < tNOT:  "not" >
|   < tOR:  "or" >
|   < tAND:  "and" >
|   < tIF:  "if" >
|   < tTHEN:  "then" >
|   < tELSE:  "else" >
|   < tWHIL:  "while" >
|   < tDO:  "do" >
|	< tCOMMA:  "," >
|	< tSCOL:  ";" >
|	< tPTOP:  "(" >
|	< tPTCL:  ")" >
|	< tSQOP:  "[" >
|	< tSQCL:  "]" >
|	< tSQUOT:  "'" >
|	< tDQUOT:  "\"" >
|   < tASIG:  ":=" >
|	< tADD:  "+" >
|	< tSUB:  "-" >
|	< tMUL:  "*" >
|	< tDIV:  "div" >
|	< tMOD:  "mod" >
|	< tGT:  ">" >
|	< tLT:  "<" >
|	< tEQ:  "=" >
|	< tGEQ:  ">=" >
|	< tLEQ:  "<=" >
|	< tDIFF:  "<>" >
|	< tTRUE:  "true" >
|	< tFALSE:  "false" >
|	< tATOI:  "char2int" >
|	< tITOA:  "int2char" >
|	< tGET:  "get" >
|	< tSKIPL:  "skip_line" >
|	< tPUT:  "put" >
|	< tPUTL:  "put_line" >
|	< tFUNC: "function" >
|	< tVAL: "val" >
|	< tREF: "ref" >
|	< tID: <LETTER> ( ("_")? (<DIGIT> | <LETTER>) )* >
}
SPECIAL_TOKEN:
{
	< ~[] >
		{
			System.err.print("LEXICAL ERROR: ");
			System.err.printf("(%d, %d): unrecognized symbol: %s", matchedToken.beginLine,
			matchedToken.beginColumn, matchedToken.image );
		}
}


//------------ Símbolo inicial de la gramática. Para análisis léxico no hace falta más
void S () : 
{
	Token t1 = new Token();
}
{
	try {
		<tPROC>
		t1 = <tID>
		<tIS>
		{
			SymbolProcedure s = new SymbolProcedure(t1.image, new ArrayList<Symbol>());
			st.insertSymbol(s);
		}
		declaracion_variables()
		declaracion_procs_funcs()
		bloque_sentencias(t1.image, Symbol.Types.PROCEDURE, Symbol.Types.UNDEFINED)
		<EOF>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		//conjSinc.add(token);
		recuperacionPanico(e.currentToken.next, conjSinc);
	} catch(AlreadyDefinedSymbolException e) {
		System.err.println("Error: " + t1.image + " ya está declarado");
	}
}

//declaracion_variables : ( declaracion <tSCOL> )*
void declaracion_variables() :
{
	
}
{
	try {
		(
			declaracion()
			<tSCOL>
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion : tipo_variable lista_vars
void declaracion() :
{
	Symbol.Types type;
}
{
	try {
		type = tipo_variable()
		lista_vars(type)
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

// tipo_variable : <tINT> | <tCHDECL> | <tBOOL>
Symbol.Types tipo_variable() :
{
	
}
{
	try {
		<tINT>
		{
			return Symbol.Types.INT;
		}
	|	<tCHDECL>
		{
			return Symbol.Types.CHAR;
		}
	|	<tBOOL>
		{
			return Symbol.Types.BOOL;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//lista_vars : variable ( <tCOMMA> variable )*
void lista_vars(Symbol.Types type) :
{
	
}
{
	try {
		variable(type)
		(
			<tCOMMA>
			variable(type)
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//variable : LOOKAHEAD(2)
//				<tID> <tACOR> <tCONST_INT> <tCCOR>
//			|	<tID>
void variable(Symbol.Types type) :
{
	Token t1;
	Token t2;
}
{
	try {
		LOOKAHEAD(2)
		t1 = <tID>
		<tSQOP>
		t2 = <tNUM>
		<tSQCL>
		{
			try {
				SymbolArray sArr = new SymbolArray(t1.image, Integer.parseInt(t2.image), type);
				st.insertSymbol(sArr);
			} catch(AlreadyDefinedSymbolException e) {
				System.err.println("Error: " + t1.image + " ya está declarado");
			}
		}
	|	t1 = <tID>
		{
			try {
				switch (type) {
					case INT : 
						SymbolInt sInt = new SymbolInt(t1.image);
						st.insertSymbol(sInt);
						break;
						
					case CHAR : 
						SymbolChar sChar = new SymbolChar(t1.image);
						st.insertSymbol(sChar);
						break;
						
					case BOOL :
						SymbolBool sBool = new SymbolBool(t1.image);
						st.insertSymbol(sBool);
						break;
					default: System.err.println("Error: " + type + " no es un tipo válido");
				}
			} catch(AlreadyDefinedSymbolException e) {
				System.err.println("Error: " + t1.image + " ya está declarado");
			}
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_procs_funcs : ( declaracion_proc_func )*
void declaracion_procs_funcs() :
{
	
}
{
	try {
		(
			declaracion_proc_func()
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_proc_func :	declaracion_proceso
//						|	declaracion_funcion
void declaracion_proc_func() :
{
	
}
{
	try {
		declaracion_proceso()
	|	declaracion_funcion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//declaracion_proceso : cabecera_proceso declaracion_variables declaracion_procs_funcs bloque_sentencias
void declaracion_proceso() :
{
	String nom;
}
{
	try {
		nom = cabecera_proceso()
		declaracion_variables()
		declaracion_procs_funcs()
		bloque_sentencias(nom, Symbol.Types.PROCEDURE, Symbol.Types.UNDEFINED)
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//cabecera_proceso : <tPROC> <tID> ( decs_parametros )? <tIS>
String cabecera_proceso() :
{
	Token t1 = new Token();
	//TODO Aqui nos cogemos un ArrayList<Symbol>, pero creo sería mejor cojer un ArrayList<Symbol.Type>, así que
	//cuando en la invocacion funcion cogemos las expresiones parametros podremos hacer una comparación más sencilla
	ArrayList<Symbol> parList = new ArrayList<Symbol>();
}
{
	try {
		<tPROC>
		t1 = <tID>
		(
			//Todo
			//Aquí tenemos 
			parList = decs_parametros()
		)?
		<tIS>
		{
			SymbolProcedure s = new SymbolProcedure(t1.image, parList);
			st.insertSymbol(s);
			st.insertBlock();
			for (Symbol par : parList) {
				st.insertSymbol(par);
			}
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		recuperacionPanico(e.currentToken.next, conjSinc);
	} catch(AlreadyDefinedSymbolException e) {
		System.err.println("Error: " + t1.image + " ya está declarado");
	}
	{
		return t1.image;
	}
}



//declaracion_funcion : cabecera_funcion declaracion_variables declaracion_procs_funcs bloque_sentencias
void declaracion_funcion() :
{
	Attributes nomYTipo;
}
{
	try {
		nomYTipo = cabecera_funcion()
		declaracion_variables()
		declaracion_procs_funcs()
		bloque_sentencias(nomYTipo.valString, Symbol.Types.FUNCTION, nomYTipo.type)
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//cabecera_funcion : <tFUNC> tipo_variable <tID> ( decs_parametros )? <tIS>
Attributes cabecera_funcion() :
{
	Attributes nomYTipo = new Attributes();
	Token t1 = new Token();
	Symbol.Types type;
	ArrayList<Symbol> parList = new ArrayList<Symbol>();
}
{
	try {
		<tFUNC>
		type = tipo_variable()
		t1 = <tID>
		(
			parList = decs_parametros()
		)?
		<tIS>
		{
			SymbolFunction s = new SymbolFunction(t1.image, parList, type);
			st.insertSymbol(s);
			st.insertBlock();
			for (Symbol par : parList) {
				st.insertSymbol(par);
			}
			nomYTipo.type = type;
			nomYTipo.valString = t1.image;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		recuperacionPanico(e.currentToken.next, conjSinc);
	} catch(AlreadyDefinedSymbolException e) {
		System.err.println("Error: " + t1.image + " ya está declarado");
	}
	{
		return nomYTipo;
	}
}

//bloque_sentencias : <tBEGIN> ( instruccion )* <tEND>
void bloque_sentencias(String nom, Symbol.Types proc_or_func, Symbol.Types retType) :
{
	boolean hayReturn = false;
	boolean aux = false;
}
{
	try {
		<tBEGIN>
		{
			System.err.println("***********************");
			System.err.println("Procesando " + nom);
			System.err.println("***********************");
		}
		(
			aux = instruccion()
			{
				hayReturn |= aux;
			}
		)*
		<tEND>
		{
			if (proc_or_func == Symbol.Types.FUNCTION && !hayReturn) {
				System.err.println("Error: No hay return en la función");
			}
			System.err.println(st.toString());
			st.removeBlock();
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//decs_parametros : <tPTOP> ( dec_parametros ( <tSCOL> dec_parametros )* )? <tPTCL>
ArrayList<Symbol> decs_parametros() :
{
	ArrayList<Symbol> partialList = new ArrayList<Symbol>();
	ArrayList<Symbol> parList = new ArrayList<Symbol>();
}
{
	try {
		<tPTOP>
		(
			partialList = dec_parametros()
			(
				<tSCOL>
				{
					parList.addAll(partialList);
				}
				partialList = dec_parametros()
			)*
		)?
		<tPTCL>
		{
			parList.addAll(partialList);
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return parList;
	}
}

//dec_parametros : clase_parametros tipo_variable id_or_array ( <tCOMMA> id_or_array )*
ArrayList<Symbol> dec_parametros() :
{
	Symbol.ParameterClass parClass = Symbol.ParameterClass.NONE;
	Symbol.Types type;
	Symbol par;
	ArrayList<Symbol> parList = new ArrayList<Symbol>();
}
{
	try {
		parClass = clase_parametros() //Si es ref o val
		type = tipo_variable()	//Si es string, int...
		par = id_or_array(type)	//El token
		{
			par.parClass = parClass;
			par.type = type;
			parList.add(par);
		}
		(
			<tCOMMA>
			par = id_or_array(type)
			{
				par.parClass = parClass;
				par.type = type;
				parList.add(par);
			}
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return parList;
	}
}

//id_or_array : LOOKAHEAD(2)
//					<tID> <tSQOP> <tNUM> <tSQCL>
//				|	<tID>
Symbol id_or_array(Symbol.Types type) :
{
	Token t1;
	Token t2;
}
{
	try {
		LOOKAHEAD(2)
		t1 = <tID>
		<tSQOP>
		t2 = <tNUM>
		<tSQCL>
		{
			return new SymbolArray(t1.image, Integer.parseInt(t2.image), type);
		}
	|	t1 = <tID>
		{
			switch (type) {
				case INT : 
					return new SymbolInt(t1.image);
					break;
					
				case CHAR : 
					return new SymbolChar(t1.image);
					break;
					
				case BOOL :
					return new SymbolBool(t1.image);
					break;
				default: System.err.println("Error: " + type + " no es un tipo válido");
			}
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//clase_parametros:	<tREF>
//				|	<tVAL>
Symbol.ParameterClass clase_parametros() :
{
	
}
{
	try {
		<tREF>
		{
			return Symbol.ParameterClass.REF;
		}
	|	<tVAL>
		{
			return Symbol.ParameterClass.VAL;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tIS);
		conjSinc.add(tPTCL);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//instruccion : 	inst_leer <tSCOL>
//				|	inst_saltar_linea <tSCOL> 
//				|	inst_escribir <tSCOL> 
//				|	inst_escribir_linea <tSCOL> 
//				|LOOKAHEAD(2)
//					inst_invoc_proc <tSCOL> 
//				|	inst_asignacion <tSCOL> 
//				|	inst_seleccion
//				|	inst_iteracion
//				|	inst_return <tSCOL>
boolean instruccion() :
{
	boolean hayReturn = false;
}
{
	try {
		inst_leer()
		<tSCOL> { return false; }
	|	inst_saltar_linea()
		<tSCOL> { return false; }
	|	inst_escribir()
		<tSCOL> { return false; }
	|	inst_escribir_linea() 
		<tSCOL> { return false; }
	|LOOKAHEAD(2)
		inst_invoc_proc()
		<tSCOL> { return false; }
	|	inst_asignacion()
		<tSCOL> { return false; }
	|	hayReturn = inst_seleccion() { return hayReturn; }
	|	hayReturn = inst_iteracion() { return hayReturn; }
	|	inst_return()
		<tSCOL> { return true; }
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_leer : <tGET> <tPTOP> asignable ( <tCOMMA> asignable )* <tPTCL>
void inst_leer() :
{
	Token token;
}
{
	try {
		<tGET>
		<tPTOP>
		asignable()
		(
			<tCOMMA>
			asignable()
		)*
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//Aqui no hay nada que comprobar
//inst_saltar_linea : <tSKIPL> <tPTOP> <tPTCL>
void inst_saltar_linea() :
{
	
}
{
	try {
		<tSKIPL>
		<tPTOP>
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_escribir : <tPUT> <tPTOP> lista_exps <tPTCL>
void inst_escribir() :
{
	
}
{
	try {
		<tPUT>
		<tPTOP>
		lista_exps()
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_escribir_linea : <tPUTL> <tPTOP> lista_exps <tPTCL>
void inst_escribir_linea() :
{
	
}
{
	try {
		<tPUTL>
		<tPTOP>
		lista_exps()
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_invoc_proc : <tID> <tPTOP> lista_exps <tPTCL>
void inst_invoc_proc() :
{
	Token funcName;
	Symbol func;
	ArrayList<Attributes> parList = new ArrayList<Attributes>();
	ArrayList<Symbol> expectedPars;
}
{
	try {
		funcName = <tID>
		{
			func = st.getSymbol(funcName.image);
			if (func.type != Symbol.Types.PROCEDURE) {
				es.deteccion(new SymbolNotFoundException(), funcName);
			}
		}
		<tPTOP>
		parList = lista_exps()
		{
			SymbolProcedure expFunc = (SymbolProcedure) func;
			expectedPars = expFunc.parList;
			if (expectedPars.size() != parList.size())
			{
				es.deteccion("El número de parámetros no coincide con el esperado", funcName);
			}
			for (int i = 0; i< parList.size(); i++)
			{
				Symbol expPar = expectedPars.get(i);
				Attributes givenPar = parList.get(i);
				if (givenPar.type != expPar.type) {
					es.deteccion("El parámetro " + i + " es " + givenPar.type + " y se esperaba: " + expPar.type, funcName);
				}
				//Si paso por referencia, tiene que ser asignable
				if (expPar.parClass == Symbol.ParameterClass.REF && !givenPar.referenciable) {
					es.deteccion("El parámetro " + i + " no es referenciable", funcName);
				}
				//Comprobar en caso de ser un vector, que tiene las mismas dimensiones.
				SymbolArray expParArray = (SymbolArray) expPar;
				SymbolArray givenParArray = (SymbolArray) givenPar.simbolo;
				
				boolean sameDimensions =	expParArray.minInd == givenParArray.minInd &&
											expParArray.maxInd == givenParArray.maxInd;
				if (givenPar.type == Symbol.Types.ARRAY && !sameDimensions) {
					es.deteccion("El parámetro " + i + " tiene rango: ("+ givenParArray.minInd + "," + givenParArray.maxInd + ")"
					+" y se esperaba: ("+ expParArray.minInd + "," + expParArray.maxInd + ")", funcName);
				}
			}
		}
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		// añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	} catch (SymbolNotFoundException e) {
		es.deteccion(e, token);
	}
}

//inst_invoc_func : <tID> <tPTOP> lista_exps <tPTCL>
Attributes inst_invoc_func() :
{
	Attributes toRet = new Attributes();
	Token funcName;
	Symbol func;
	ArrayList<Attributes> parList = new ArrayList<Attributes>();
	ArrayList<Symbol> expectedPars;
	SymbolFunction expFunc;
}
{
	try {
		funcName = <tID>
		{
			func = st.getSymbol(funcName.image);
			if (func.type != Symbol.Types.FUNCTION) {
				es.deteccion(new SymbolNotFoundException(), funcName);
			}
		}
		<tPTOP>
		parList = lista_exps()
		{
			expFunc = (SymbolFunction) func;
			expectedPars = expFunc.parList;
			if (expectedPars.size() != parList.size())
			{
				es.deteccion("El número de parámetros no coincide con el esperado", funcName);
			}
			for (int i = 0; i< parList.size(); i++)
			{
				Symbol expPar = expectedPars.get(i);
				Attributes givenPar = parList.get(i);
				if (givenPar.type != expPar.type) {
					es.deteccion("El parámetro " + i + " es " + givenPar.type + " y se esperaba: " + expPar.type, funcName);
				}
				//Si paso por referencia, tiene que ser asignable
				if (expPar.parClass == Symbol.ParameterClass.REF && !givenPar.referenciable) {
					es.deteccion("El parámetro " + i + " no es referenciable", funcName);
				}
				//Comprobar en caso de ser un vector, que tiene las mismas dimensiones.
				SymbolArray expParArray = (SymbolArray) expPar;
				SymbolArray givenParArray = (SymbolArray) givenPar.simbolo;
				
				boolean sameDimensions =	expParArray.minInd == givenParArray.minInd &&
											expParArray.maxInd == givenParArray.maxInd;
				if (givenPar.type == Symbol.Types.ARRAY && !sameDimensions) {
					es.deteccion("El parámetro " + i + " tiene rango: ("+ givenParArray.minInd + "," + givenParArray.maxInd + ")"
					+" y se esperaba: ("+ expParArray.minInd + "," + expParArray.maxInd + ")", funcName);
				}
			}
		}
		<tPTCL>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		// añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	} catch (SymbolNotFoundException e) {
		es.deteccion(e, token);
	}
	{
		toRet.type = expFunc.returnType;
		toRet.referenciable = false;
		toRet.simbolo = func;
		return toRet;
	}
}


//inst_asignacion : asignable <tASIG> expresion
void inst_asignacion() :
{
	
}
{
	try {
		asignable()
		<tASIG>
		expresion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_seleccion :	<tIF> expresion <tTHEN> ( instruccion )* resto_if <tEND>
boolean inst_seleccion() :
{
	boolean hayReturn1 = false;
	boolean hayReturn2 = false;
	boolean aux = false;
	Attributes attr;
}
{
	try {
		<tIF>
		attr = expresion() {
			if (attr.type != Symbol.Types.BOOL)
				es.deteccion("El tipo de la expresion tiene que ser booleano", token);
		}
		<tTHEN>
		(
			aux = instruccion()
			{
				hayReturn1 |= aux;
			}
		)*
		(
			aux = resto_if()
			{
				hayReturn2 |= aux;
			}
		)?
		<tEND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return hayReturn1 && hayReturn2;
	}
}

//resto_if : <tELSE> ( instruccion )*
boolean resto_if() :
{
	boolean hayReturn = false;
	boolean aux = false;
}
{
	try {
		<tELSE>
		(
			aux = instruccion()
			{
				hayReturn |= aux;
			}
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return hayReturn;
	}
}

//asignable : LOOKAHEAD(2)
//				<tID> <tSQOP> expresion <tSQCL>
//			|	<tID>
//Un asignable es el nombre de una variable escalar o de un array con un indice (que puede ser una expresion)
//
//Desde asignable tenemos que comprobar que el nombre existe, así que no haga falta mirarlo en los varios lugares donde llamamos esta funcion

void asignable() :
{
	Token token;
	Attributes attr;
}
{
	try {
	LOOKAHEAD(3)
		token = <tID> {
			if (!st.containsSymbol(token.image)) {
				throw new SymbolNotFoundException();
			}
		}

		<tSQOP>
		attr = expresion() {
			if (attr.type != Symbol.Types.INT) {
				es.deteccion("Tipo de indice incorrecto, esperaba un entero", token);
			}
		}
		<tSQCL>
	|	token = <tID> {
			if (!st.containsSymbol(token.image)) {
				es.deteccion(new SymbolNotFoundException(), token);
			}
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//inst_iteracion : <tWHIL> expresion <tDO> ( instruccion )* <tEND>
boolean inst_iteracion() :
{
	boolean hayReturn = false;
	boolean aux = false;
}
{
	try {
		<tWHIL>
		expresion()
		<tDO>
		(
			aux = instruccion()
			{
				hayReturn |= aux;
			}
		)*
		<tEND>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return hayReturn;
	}
}

//inst_return : <tRET> expresion
void inst_return() :
{
	
}
{
	try {
		<tRET>
		expresion()
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//lista_exps : ( expresion ( <tCOMMA> expresion )* )?
ArrayList<Attributes> lista_exps() :
{
	ArrayList<Attributes> parList = new ArrayList<Attributes>();
	Attributes attr;
}
{
	try {
		(
			attr = expresion() { 
				parList.add(attr);
			}
			(
				<tCOMMA>
				attr = expresion() { 
				parList.add(attr);
				}
			)*
		)?
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return parList;
	}
}

//expresion : expresion_simple ( operador_relacional expresion_simple )?
Attributes expresion() :
{
	Attributes attrExpresion1;
	Symbol.Types tipo_operador = null;
	Attributes attrExpresion2 = null;
	Attributes retAttribute = new Attributes();
	retAttribute.type = Symbol.Types.BOOL;
	retAttribute.referenciable = false;
}
{
	try {
		attrExpresion1 = expresion_simple()
		(
			operador_relacional()
			attrExpresion2 = expresion_simple()
			{
				if (attrExpresion2.type != attrExpresion1.type)
					es.deteccion("Tipos incoerentes entre variables", token);
				attrExpresion1.referenciable = false;
			}
			{
				return retAttribute; //Si hay operador relacional devuelve un Attributes booleano
			}
		)?
		{
			return attrExpresion1; //Si no hay operador relacional
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//operador_relacional :	<tEQ> =
//					|	<tLT> <
//					|	<tGT> >
//					|	<tLEQ> <=
//					|	<tGEQ> >=
//					|	<tDIFF> <>
void operador_relacional() :
{
	
}
{
	try {
		<tEQ>
	|	<tLT>
	|	<tGT>
	|	<tLEQ>
	|	<tGEQ>
	|	<tDIFF>
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//expresion_simple : ( <tADD> | <tSUB> )? termino ( operador_aditivo termino )*
Attributes expresion_simple() :
{
	Attributes a = new Attributes();
	Attributes a2 = null;
}
{
	try {
		(
			<tADD> 	{
				a2 = new Attributes();
			}
		|	<tSUB> {
				a2 = new Attributes();
			}
		)?
		a = termino()
		{
			if (a2!=null){
				if (a.type != Symbol.Types.INT)
					es.deteccion("La variable despues de +/- tiene que ser intera", token);
			}
			a.referenciable = false;
		}
		(
			a2 = operador_aditivo() {
				if (a2.type != a.type)
					es.deteccion("Los tipos del operando y el termino no coinciden", token);
				a.referenciable = false;
			}
			a2 = termino() {
				if (a2.type != a.type)
					es.deteccion("Los tipos de los terminos de la suma no coinciden", token);
				a.referenciable = false;
			}
		)*
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return a;
	}
}

//operador_aditivo :	<tADD>
//					|	<tSUB>
//					|	<tOR>
Attributes operador_aditivo() :
{
	Attributes a = new Attributes();
}
{
	try {
		<tADD> {
			a.type = Symbol.Types.INT;
		}
	|	<tSUB> {
			a.type = Symbol.Types.INT;
		}
	|	<tOR> {
			a.type = Symbol.Types.BOOL;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return a;
	}
}

//termino : factor ( operador_multiplicativo factor )*
Attributes termino() :
{
	Attributes a;
	Attributes a2;
}
{
	try {
		a = factor()
		(
			a2 = operador_multiplicativo() {
				if (a2.type != a.type)
					es.deteccion("Los tipos del operando y el operator no coinciden", token);
			}
			a2 = factor() {
				if (a2.type != a.type)
					es.deteccion("Los tipos de los operandos no coinciden: " + a.type + " vs " + a2.type, token);
				a.referenciable = false;
			}
		)*
		{
			return a;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
}

//operador_multiplicativo :	<tMUL>
//						|	<tMOD>
//						|	<tDIV>
//						|	<tAND>
Attributes operador_multiplicativo() :
{
	Attributes a = new Attributes();
}
{
	try {
		<tMUL> {
			a.type = Symbol.Types.INT;
		}
	|	<tMOD> {
			a.type = Symbol.Types.INT;
		}
	|	<tDIV> {
			a.type = Symbol.Types.INT;
		}
	|	<tAND> {
			a.type = Symbol.Types.BOOL;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return a;
	}
}

//factor :	<tNOT> factor
//		|	<tPTOP> expresion <tPTCL>
//		|	<tINT2CHAR> <tPTOP> expresion <tPTCL>
//		|	<tCHAR2INT> <tPTOP> expresion <tPTCL>
//		|	LOOKAHEAD(2)  <tID> <tPTOP> lista_exps <tPTCL> Esta es una llamada a funcion
//		|	LOOKAHEAD(2)  <tID> <tSQOP> expresion <tSQCL>  componente array
//		|	<tID>
//		|	<tNUM>
//		|	<tCHAR>
//		|	<tSTRING>
//		|	<tTRUE>
//		|	<tFALSE>
Attributes factor() :
{
	Attributes a = new Attributes();
	Attributes temp;
	Token t;
	Symbol s;
}
{
	try {
		<tNOT>
		a = factor(){ 
			if (a.type != Symbol.Types.BOOL) {
				es.deteccion("El tipo de la expresion tiene que ser booleano", token);
			}
			a.referenciable = false;
		}
	|	<tPTOP>
		a = expresion()
		<tPTCL>
	|	<tITOA>
		<tPTOP>
		//A int to ascii tienes que pasarle un int
		temp = expresion()
		{
			if (temp.type != Symbol.Types.INT) {
				es.deteccion("El tipo de la expresion tiene que ser intero", token);
			}
			a.type = Symbol.Types.CHAR;
			a.referenciable = false;
		}
		<tPTCL>
	|	<tATOI>
		<tPTOP>
		//A ascii to int tienes que pasarle un char
		temp = expresion()
		{
			if (temp.type != Symbol.Types.CHAR) {
				es.deteccion("El tipo de la expresion tiene que ser character", token);
			}
			a.type = Symbol.Types.INT;
			a.referenciable = false;
		}
		<tPTCL>
	|	LOOKAHEAD(2) 
		a = inst_invoc_func()
	|	LOOKAHEAD(2) 
		t = <tID>	//Esto es id de array
		{
			s = st.getSymbol(t.image);
			if (!st.containsSymbol(t.image)) {
				es.deteccion(new SymbolNotFoundException(), t);
			} else {
				if (s.type != Symbol.Types.ARRAY) {
					es.deteccion(t.image + "no es identificador de array", t);
				}
				a.type = s.type;
				a.referenciable = true;
				a.name = t.image;
				a.simbolo = st.getSymbol(t.image);
			}
		}
		<tSQOP>
		temp = expresion() { 
			if (a.type != Symbol.Types.INT)
				es.deteccion("El indice de elemento de array tiene que ser intero", t);
		}
		<tSQCL>
	|	t = <tID>
		{
			if (!st.containsSymbol(t.image)) {
				es.deteccion(new SymbolNotFoundException(), t);
			} else {
				s = st.getSymbol(t.image);
				if (s.type == Symbol.Types.ARRAY || s.type == Symbol.Types.FUNCTION || s.type == Symbol.Types.PROCEDURE) {
					es.deteccion(t.image + "no puede ser utilizado como variable simple", t);
				}
				a.type = s.type;
				a.referenciable = true;
				a.name = t.image;
				a.simbolo = s;
			}
		}
	|	<tNUM>
		{
			a.type = Symbol.Types.INT;
			a.referenciable = false;
		}
	|	<tCHAR>
		{
			a.type = Symbol.Types.CHAR;
			a.referenciable = false;
		}
	|	<tSTRING>
		{
			a.type = Symbol.Types.STRING;
			a.referenciable = false;
		}
	|	<tTRUE>
		{
			a.type = Symbol.Types.BOOL;
			a.referenciable = false;
		}
	|	<tFALSE>
		{
			a.type = Symbol.Types.BOOL;
			a.referenciable = false;
		}
	} catch (ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		//añadir tokens al conj de sincronización
		conjSinc.add(tEND);
		conjSinc.add(tSCOL);
		conjSinc.add(tPTCL);
		conjSinc.add(tCOMMA);
		conjSinc.add(tSQCL);
		recuperacionPanico(e.currentToken.next, conjSinc);
	}
	{
		return a;
	}
}

JAVACODE //------------------------------------ 
//Informa sobre la excepción, y calcula el conjunto de tokens esperados Set infoParseException(ParseException e) {
Set<Integer> infoParseException(ParseException e) {
    Set<Integer> esperados = new HashSet<Integer>(); 
    System.err.println("ERROR_SINT: ('" + e.currentToken.next.image + ", " + e.currentToken.next.beginLine
        + "," + e.currentToken.next.beginColumn + ")");
    System.err.println("Se esperaba uno de los siguientes tokens:");
    for(int i = 0; i<e.expectedTokenSequences.length; i++) {
        esperados.add(e.expectedTokenSequences[i][0]);
        System.err.println("\t" + adacConstants.tokenImage[e.expectedTokenSequences[i][0]]);
    }
    return esperados;
}

JAVACODE //--------- 
void recuperacionPanico(Token tInesperado, Set <Integer> conjSinc) {
    // System.err.println(sep); 
    System.err.println("----> Recupero en modo pánico: "
        + "\n----> saltando todo hasta token de conjunto de sincronización"); 
    System.err.println("Lexema inesperado: " + tInesperado.image);
    Token t = getNextToken();
    while (!conjSinc.contains(t. kind) && t.kind != EOF){ 
        System.err.println("Descartando token ("
        + adacConstants.tokenImage[t.kind]
        + "," + t.image + ")"); t = getNextToken();
    }
}